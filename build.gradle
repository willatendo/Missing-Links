plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version "${forgegradle_version}"
	id 'com.github.johnrengelman.shadow' version "${shadow_version}"
}

apply plugin: 'net.minecraftforge.gradle'

version = '3.1.0'
group = 'missinglinks'
archivesBaseName = 'missinglinks'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: minecraft_version
	
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    
    runs {
        client {
            workingDirectory project.file('run')

            mods {
                missinglinks {
                    source sourceSets.main
                }
            }
        }
        
        data {
            workingDirectory project.file('run')
            args '--mod', 'missinglinks', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
                    
            mods {
                missinglinks {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}


repositories {
	maven { url 'https://maven.tterrag.com/' }
}

configurations {
	shade
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    
    def registrate = "com.tterrag.registrate:Registrate:MC${major_version}-${registrate_version}"
	implementation fg.deobf(registrate)
	shade registrate
}

jar {
    manifest {
        attributes([
            "Specification-Title": "missinglinks",
            "Specification-Vendor": "Willatendo",
            "Specification-Version": "3.1.0",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Willatendo",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

shadowJar {
	classifier = ''
	configurations = [project.configurations.shade]
	relocate 'com.tterrag.registrate', 'missinglinks.repack.registrate'
}

reobf {
	shadowJar {}
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar


jar.finalizedBy('reobfJar') 